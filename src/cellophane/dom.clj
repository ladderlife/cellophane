(ns cellophane.dom
  (:refer-clojure :exclude [map meta time use])
  (:require [clojure.string :as str]
            [cellophane.protocols :as p])
  (:import [java.util.zip Adler32]))

;; ===================================================================
;; Checksums (data-react-checksum)

;; Not equal to React's optimized version of adler32. See
;; https://github.com/facebook/react/blob/3b96650/src/shared/utils/adler32.js
(defn checksum [markup]
  (let [chk (Adler32.)
        bytes (.getBytes markup)]
    (.update chk bytes 0 (count bytes))
    (.getValue chk)))


;; ===================================================================
;; DOM render

(def tags
  '[a
    abbr
    address
    area
    article
    aside
    audio
    b
    base
    bdi
    bdo
    big
    blockquote
    body
    br
    button
    canvas
    caption
    cite
    code
    col
    colgroup
    data
    datalist
    dd
    del
    details
    dfn
    dialog
    div
    dl
    dt
    em
    embed
    fieldset
    figcaption
    figure
    footer
    form
    h1
    h2
    h3
    h4
    h5
    h6
    head
    header
    hr
    html
    i
    iframe
    img
    ins
    kbd
    keygen
    label
    legend
    li
    link
    main
    map
    mark
    menu
    menuitem
    meta
    meter
    nav
    noscript
    object
    ol
    optgroup
    output
    p
    param
    picture
    pre
    progress
    q
    rp
    rt
    ruby
    s
    samp
    script
    section
    small
    source
    span
    strong
    style
    sub
    summary
    sup
    table
    tbody
    td
    tfoot
    th
    thead
    time
    title
    tr
    track
    u
    ul
    var
    video
    wbr

    ;; svg
    circle
    clipPath
    ellipse
    g
    line
    mask
    path
    pattern
    polyline
    rect
    svg
    text
    defs
    linearGradient
    polygon
    radialGradient
    stop
    tspan
    use

    ;; hand-generated by Om
    input
    textarea
    options
    select])

(defn escape-html
  "Change special characters into HTML character entities."
  [text]
  {:pre [(string? text)]
   :post [(string? %)]}
  (.. ^String (clojure.core/name text)
    (replace "&"  "&amp;")
    (replace "<"  "&lt;")
    (replace ">"  "&gt;")
    (replace "\"" "&quot;")))

(defn xml-attribute [name value]
  (str " " (clojure.core/name name) "=\"" (escape-html value) "\""))

(def no-suffix
  #{"animationIterationCount" "boxFlex" "boxFlexGroup" "boxOrdinalGroup"
    "columnCount" "fillOpacity" "flex" "flexGrow" "flexPositive" "flexShrink"
    "flexNegative" "flexOrder" "fontWeight" "lineClamp" "lineHeight" "opacity"
    "order" "orphans" "stopOpacity" "strokeDashoffset" "strokeOpacity"
    "strokeWidth" "tabSize" "widows" "zIndex" "zoom"})

(defn format-styles [styles]
  (letfn [(->kebab-case [s]
            (->> (str/split s #"(?=[A-Z])")
              (clojure.core/map #(reduce str %))
              (clojure.core/map str/lower-case)
              (str/join "-")))
          (coerce-value [k v]
            (cond-> v
              (and (number? v)
                   (not (contains? no-suffix k))) (str "px")))]
    (reduce (fn [s [k v]]
              (let [k (name k)]
                (str s (->kebab-case k) ":" (coerce-value k v) ";")))
      "" styles)))

(defn render-attribute [[name value]]
  (cond
    (true? value) (str " " (clojure.core/name name))
    (fn? value) ""
    (not value) ""
    (= name :style) (xml-attribute name (format-styles value))
    :else (xml-attribute name value)))

(defn render-attr-map [attrs]
  (apply str
    (clojure.core/map render-attribute (sort-by key attrs))))

(def ^{:doc "A list of elements that must be rendered without a closing tag."
       :private true}
  void-tags
  #{"area" "base" "br" "col" "command" "embed" "hr" "img" "input" "keygen" "link"
    "meta" "param" "source" "track" "wbr"})

(defn container-tag?
  "Returns true if the tag has content or is not a void tag. In non-HTML modes,
  all contentless tags are assumed to be void tags."
  [tag content]
  (or content
    (and (not (void-tags tag)))))

(defn react-id-str [react-id]
  (assert (vector? react-id))
  (str "." (str/join "." react-id)))

(defn render-element
  "Render an tag vector as a HTML element string."
  [{:keys [tag attrs react-id children]}]
  (assert react-id)
  (let [attrs (assoc attrs :data-reactid (react-id-str react-id))]
    (if (container-tag? tag (seq children))
      (str "<" tag (render-attr-map attrs) ">"
        (apply str (clojure.core/map p/-render-to-string children))
        "</" tag ">")
      (str "<" tag (render-attr-map attrs) ">"))))

(defrecord Element [tag attrs react-id children]
  p/IReactDOMElement
  (-render-to-string [this]
    (render-element this))

  p/IReactChildren
  (-children [this] children))

(defrecord Text [s]
  p/IReactDOMElement
  (-render-to-string [this]
    (assert (string? s))
    s))

(defn text-node
  "HTML text node"
  [s]
  (map->Text {:s s}))

(defn element
  "Creates a dom node."
  [{:keys [tag attrs children] :as elem}]
                                        ;{:post [(valid-element? %)]}
  (assert (name tag))
  (assert (or (nil? attrs) (map? attrs)) (format "elem %s attrs invalid" elem))
  (let [children (doall (->> (clojure.core/map
                               (fn [c]
                                 (cond
                                   (satisfies? p/IReactDOMElement c) c
                                   (satisfies? p/IReactComponent c) c
                                   (string? c) (text-node c)
                                   (nil? c) nil
                                   :else (do
                                           (println "invalid child element:" c (class c))
                                           (assert false)))) (flatten children))
                          (filter identity)))]

    (map->Element {:tag (name tag)
                   :attrs attrs
                   :children children})))

(defn gen-tag-fn [tag]
  `(defn ~tag [~'attrs & ~'children]
     (element {:tag (quote ~tag)
               :attrs ~'attrs
               :children ~'children})))

(defmacro gen-all-tags []
  `(do
     ~@(clojure.core/map gen-tag-fn tags)))

(gen-all-tags)


(defn assign-react-ids
  ([elem]
   (assign-react-ids elem [0]))
  ([elem id]
   (assert (vector? id))
   (let [elem (assoc elem :react-id id)]
     (update-in elem [:children] (fn [children]
                                   (map-indexed (fn [i c]
                                                  (assign-react-ids c (conj id i))) children))))))

(defn render-to-str [class]
  {:pre [(satisfies? p/IReactComponent class)]}
  (let [element (p/-render class)
        element (assign-react-ids element)]
    (p/-render-to-string element)))
