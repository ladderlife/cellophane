(ns cellophane.dom
  (:refer-clojure :exclude [map meta time use])
  (:require [cellophane.protocols :as p]))

(def tags
  '[a
    abbr
    address
    area
    article
    aside
    audio
    b
    base
    bdi
    bdo
    big
    blockquote
    body
    br
    button
    canvas
    caption
    cite
    code
    col
    colgroup
    data
    datalist
    dd
    del
    details
    dfn
    dialog
    div
    dl
    dt
    em
    embed
    fieldset
    figcaption
    figure
    footer
    form
    h1
    h2
    h3
    h4
    h5
    h6
    head
    header
    hr
    html
    i
    iframe
    img
    ins
    kbd
    keygen
    label
    legend
    li
    link
    main
    map
    mark
    menu
    menuitem
    meta
    meter
    nav
    noscript
    object
    ol
    optgroup
    output
    p
    param
    picture
    pre
    progress
    q
    rp
    rt
    ruby
    s
    samp
    script
    section
    small
    source
    span
    strong
    style
    sub
    summary
    sup
    table
    tbody
    td
    tfoot
    th
    thead
    time
    title
    tr
    track
    u
    ul
    var
    video
    wbr

    ;; svg
    circle
    clipPath
    ellipse
    g
    line
    mask
    path
    pattern
    polyline
    rect
    svg
    text
    defs
    linearGradient
    polygon
    radialGradient
    stop
    tspan
    use

    ;; hand-generated by Om
    input
    textarea
    options
    select])

(defn escape-html
  "Change special characters into HTML character entities."
  [text]
  {:pre [(string? text)]
   :post [(string? %)]}
  (.. ^String (clojure.core/name text)
    (replace "&"  "&amp;")
    (replace "<"  "&lt;")
    (replace ">"  "&gt;")
    (replace "\"" "&quot;")))

(defn xml-attribute [name value]
  (str " " (clojure.core/name name) "=\"" (escape-html value) "\""))

(defn render-attribute [[name value]]
  (cond
    (true? value) (str " " (clojure.core/name name))
    (fn? value) ""
    (not value) ""
    :else (xml-attribute name value)))

(defn render-attr-map [attrs]
  (apply str
         (clojure.core/map render-attribute (sort-by key attrs))))

(def ^{:doc "A list of elements that must be rendered without a closing tag."
       :private true}
  void-tags
  #{"area" "base" "br" "col" "command" "embed" "hr" "img" "input" "keygen" "link"
    "meta" "param" "source" "track" "wbr"})

(defn container-tag?
  "Returns true if the tag has content or is not a void tag. In non-HTML modes,
  all contentless tags are assumed to be void tags."
  [tag content]
  (or content
    (and (not (void-tags tag)))))

(defn render-element [{:keys [tag attrs children]}]
  (if (container-tag? tag (seq children))
    (str "<" tag (render-attr-map attrs) ">"
         (apply str (clojure.core/map p/-render-to-string children))
         "</" tag ">")
    (str "<" tag (render-attr-map attrs) ">")))

(defrecord Element [tag attrs children]
  p/ReactDOMElement
  (-children [this] children)
  (-render-to-string [this]
    (render-element this)))

(defrecord Text [s]
  p/ReactDOMElement
  (-children [this]
    nil)
  (-render-to-string [this]
    (assert (string? s))
    s))

(defn text-node
  "HTML text node"
  [s]
  (map->Text {:s s}))

(defn element
  "Creates a dom node."
  [{:keys [tag attrs children] :as elem}]
                                        ;{:post [(valid-element? %)]}
  (assert (name tag))
  (assert (or (nil? attrs) (map? attrs)) (format "elem %s attrs invalid" elem))
  (let [children (doall (->> (clojure.core/map
                               (fn [c]
                                 (cond
                                   (satisfies? p/ReactDOMElement c) c
                                   (satisfies? p/ReactComponent c) c
                                   (string? c) (text-node c)
                                   (nil? c) nil
                                   :else (do
                                           (println "invalid child element:" c (class c))
                                           (assert false)))) children)
                          (filter identity)))]

    (map->Element {:tag (name tag)
                   :attrs attrs
                   :children children})))

(defn gen-tag-fn [tag]
  `(defn ~tag [~'attrs & ~'children]
     (element {:tag (quote ~tag)
               :attrs ~'attrs
               :children ~'children})))

(defmacro gen-all-tags []
  `(do
     ~@(clojure.core/map gen-tag-fn tags)))

(gen-all-tags)

(defn render-to-string [class]
  {:pre [(satisfies? p/ReactComponent class)]}
  (let [element (p/-render class)]
    (p/-render-to-string element)))
